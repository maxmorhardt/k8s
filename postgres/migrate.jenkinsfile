import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

def getBackupFilename() {
	def formattedDate = LocalDateTime.now().format(DateTimeFormatter.ofPattern("yyyy-MM-dd"))
	def fileName = "postgres-backup-${formattedDate}.sql"

	echo "Created backup file name: ${fileName}"
	return fileName
}

pipeline {
	agent {
		kubernetes {
			inheritFrom 'default'
			defaultContainer 'buildpack'
		}
	}

	options {
		timeout(time: 30, unit: 'MINUTES')
		buildDiscarder(logRotator(numToKeepStr: '5'))
	}

	parameters {
		string(name: 'HELM_VERSION', defaultValue: params.HELM_VERSION ?: '0.0.1', description: 'Helm version', trim: true)
		booleanParam(name: 'CONFIRM_MIGRATION', defaultValue: false, description: 'Confirm PostgreSQL major version migration (will cause downtime)')
	}

	environment {
		GITHUB_URL = 'https://github.com/maxmorhardt/k8s'

		DOCKER_REGISTRY = 'docker.io'
		DOCKER_REGISTRY_FULL = "oci://${env.DOCKER_REGISTRY}"

		APP_NAME = "db"
		NAMESPACE = "maxstash-global"
		POD_NAME = "${APP_NAME}-postgresql-0"
		
		KUBECONFIG = credentials('kube-config')
		POSTGRES = credentials('postgres')
	}

	stages {
		stage('Confirm Migration') {
			steps {
				script {
					if (!params.CONFIRM_MIGRATION) {
						error('Migration not confirmed. Check CONFIRM_MIGRATION parameter to proceed.')
					}

					echo "PostgreSQL Major Version Migration"
				}
			}
		}

		stage('Setup') {
			steps {
				script {
					checkout scmGit(
						branches: [[
							name: "$BRANCH_NAME"
						]],
						userRemoteConfigs: [[
							credentialsId: 'github',
							url: "$GITHUB_URL"
						]]
					)

					echo "APP_NAME: $APP_NAME"
					echo "NAMESPACE: $NAMESPACE"
					echo "BRANCH: $BRANCH_NAME"
					echo "HELM_VERSION: $HELM_VERSION"
				}
			}
		}

		stage('Backup Database') {
			steps {
				dir('postgres') {
					script {
						env.BACKUP_FILE = getBackupFilename()

						sh """
							kubectl exec ${POD_NAME} -n ${NAMESPACE} -- bash -c 'PGPASSWORD=${POSTGRES_PSW} pg_dumpall -U ${POSTGRES_USR} > /tmp/backup.sql'
							kubectl cp ${NAMESPACE}/${POD_NAME}:/tmp/backup.sql ${BACKUP_FILE}
						"""

						archiveArtifacts artifacts: "$BACKUP_FILE", fingerprint: true
					}
				}
			}
		}

		stage('Uninstall Legacy Postgres') {
			steps {
				dir('postgres') {
					script {
						sh "helm uninstall $APP_NAME -n $NAMESPACE"
					}
				}
			}
		}

		stage('Delete Legacy Data') {
			steps {
				dir('postgres') {
					script {
						sh """
							kubectl delete pvc data-${POD_NAME} -n $NAMESPACE
							kubectl delete pv postgres

							kubectl apply -f cleanup.yaml -n $NAMESPACE
							kubectl wait --for=condition=complete job/cleanup-postgres-data -n $NAMESPACE --timeout=300s
							kubectl delete job cleanup-postgres-data -n $NAMESPACE
						"""
					}
				}
			}
		}

		stage('Deploy New Postgres') {
			steps {
				dir('postgres') {
					script {
						sh """
							kubectl apply -f storage.yaml

							helm upgrade $APP_NAME $DOCKER_REGISTRY_FULL/bitnamicharts/postgresql \
								--install \
								--atomic \
								--debug \
								--history-max=3 \
								--namespace $NAMESPACE \
								--version $HELM_VERSION \
								--values values.yaml
						"""
					}
				}
			}
		}

		stage('Restore Database') {
			steps {
				dir('postgres') {
					script {
						sh """
							kubectl cp ${BACKUP_FILE} ${NAMESPACE}/${POD_NAME}:/tmp/restore.sql
							kubectl exec ${POD_NAME} -n ${NAMESPACE} -- bash -c 'PGPASSWORD=${POSTGRES_PSW} psql -U ${POSTGRES_USR} -d postgres -f /tmp/restore.sql'
						"""
					}
				}
			}
		}
	}
}
